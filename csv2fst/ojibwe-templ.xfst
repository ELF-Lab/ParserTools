! There are flags at morpheme boundaries.
set flag-is-epsilon ON

define Sigma [ a | b | c | d | e | g | h | i | k | m | n | o | s | t | w | y | z ] ;
define Cons [ b | c | d | g | h | k | m | n | s | t | w | y | z ] ;
define Vowel [ a | e | i | o ] ;

! Sometimes there may be more than one consecutive
! morpheme boundary. Reduce those to 1.
define PrefixBoundaryCleanup ["<<"]+ @-> "<<" ;
define SuffixBoundaryCleanup [">>"]+ @-> ">>" ;

define dDeletion d -> 0 || _ ">>" Cons ;

define amRule1 m -> a || a _ ">>" [m|n] ;

define amRule2 m -> n || a _ ">>" [z|g] ;

define nDeletion n -> 0 || [ i | o o ] _ ">>" Cons ;

define vowelDeletion [ Vowel - e ] -> 0 || Cons _ ">>" .#. ;

define wDeletion w -> 0 || _ (">>") .#. ;

! Sources:
! https://ojibwe.lib.umn.edu/main-entry/g-pf
! https://ojibwe.lib.umn.edu/main-entry/g-pf
! https://ojibwegrammar.langsci.wisc.edu/Assets/Pdfs/InflAnishPersonPrefixes1.02.pdf
define PrefixDInsertion [..] -> d || [ [ n | g ] i | o ] _ "<<" Vowel ;
define PrefixOLengthening o -> {oo} || [ [ n | g ] i | o ] d "<<" _ \o ; 

define cleanUp ["<<"|">>"] -> 0 ; 

read lexc VAIO_CNJ.lexc
define VAIOCNJ ;

read lexc VAIO_IMP.lexc
define VAIOIMP ;

read lexc VAIO_IND.lexc
define VAIOIND ;

read lexc VAI_CNJ.lexc
define VAICNJ ;

read lexc VAI_IMP.lexc
define VAIIMP ;

read lexc VAI_IND.lexc
define VAIIND ;

read lexc VII_CNJ.lexc
define VIICNJ ;

read lexc VII_IND.lexc
define VIIIND ;

read lexc VTA_CNJ.lexc
define VTACNJ ;

read lexc VTA_IMP.lexc
define VTAIMP ;

read lexc VTA_IND.lexc
define VTAIND ;

read lexc VTI_CNJ.lexc
define VTICNJ ;

read lexc VTI_IMP.lexc
define VTIIMP ;

read lexc VTI_IND.lexc
define VTIIND ;

!read lexc lexc_source/verb_prefixes.lexc
!define VPrefixes ;
!read regex "<<" VPrefixes ;
!define Prefixes ;

regex [ VAIOCNJ |
      	VAIOIMP |
	VAIOIND |
	VAICNJ |
	VAIIMP |
	VAIIND |
	VIICNJ |
	VIIIND |
	VTACNJ |
	VTAIMP |
	VTAIND |
	VTICNJ |
	VTIIMP |
	VTIIND ] ;

! FIXME: We need an "insert preverbs here" flag in the lexicon.
! We don't want to substitute "<<" because that causes problems
! later.
substitute defined Prefixes for "<<"
define Lexicon ;

read regex cleanUp.i .o. Lexicon .o. [ dDeletion .o.
     	   	                       amRule1 .o.
     	   	                       amRule2 .o.
			               vowelDeletion .o.
			               wDeletion .o.
			               nDeletion .o.
			               PrefixDInsertion .o.
			               PrefixOLengthening .o.
			               cleanUp ] ;

!define Analyzer

! Closed classes
!read lexc lexc_source/numerals.lexc
!define Numerals ;

!read lexc lexc_source/particles.lexc
!define Particles ;

!read lexc lexc_source/adverbs.lexc
!define Adverbs ;

!read lexc lexc_source/quantifiers.lexc
!define Quantifiers ;

!read lexc lexc_source/pronouns.lexc
!define Pronouns ;

!read regex [ Analyzer |
!             Numerals |
!	     Particles |
!	     Adverbs |
!	     Quantifiers |
!	     Pronouns ];

save stack ojibwe.fomabin